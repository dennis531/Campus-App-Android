apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: '../findbugs.gradle'
apply from: '../pmd.gradle'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.1'
    buildTypes {
        all {
            proguardFiles(file('../proguard').listFiles())
            proguardFile getDefaultProguardFile('proguard-android.txt')
            minifyEnabled true
            useProguard true
        }
        debug.debuggable true
        release.debuggable false
    }
    defaultConfig {
        applicationId "de.tum.in.tumcampus"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 999999
        versionName "1.5.2-dev"
    }
    lintOptions {
        checkAllWarnings true
        htmlReport false
        abortOnError false
        lintConfig new File("lint.xml")
        disable 'GoogleAppIndexingWarning',
                'TrulyRandom',
                'ContentDescription',
                'UnusedResources',    // unused resources get purged by shrinkResources
                'SelectableText'      // disabled by default
    }
    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    sourceSets {
        unitTest {
            java {
                srcDir 'src/test/java'
            }
        }
    }
}

def androidSupportVersion = '27.0.2'
def retrofitVersion = '2.3.0'
def firebaseVersion = '11.8.0'
def roomVersion = '1.0.0'

configurations.all {
    resolutionStrategy {
        force "com.android.support:design:$androidSupportVersion"
        force 'com.google.code.findbugs:jsr305:3.0.1'
    }
}

dependencies {
    implementation "com.android.support:cardview-v7:$androidSupportVersion"
    implementation "com.android.support:design:$androidSupportVersion"
    implementation "com.android.support:preference-v7:$androidSupportVersion"
    implementation "com.google.android.gms:play-services-base:$firebaseVersion"
    implementation "com.google.firebase:firebase-messaging:$firebaseVersion"
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.google.guava:guava:23.2-android'
    implementation 'se.emilsjolander:stickylistheaders:2.7.0'

    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation 'com.github.chrisbanes:PhotoView:1.3.0'
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
    implementation('org.simpleframework:simple-xml:2.7.1') {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'xpp3', module: 'xpp3'
    }
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.3'
    implementation 'com.github.alamkanak:android-week-view:1.2.6'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'net.danlew:android.joda:2.9.9.1'
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.0'
    implementation 'com.github.barteksc:android-pdf-viewer:2.5.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
    implementation "android.arch.persistence.room:rxjava2:$roomVersion"
    implementation "android.arch.persistence.room:runtime:$roomVersion"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
    implementation "android.arch.persistence.room:rxjava2:$roomVersion"
    implementation "android.arch.lifecycle:runtime:$roomVersion"
    implementation "android.arch.lifecycle:extensions:$roomVersion"
    annotationProcessor "android.arch.lifecycle:compiler:$roomVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$roomVersion"
    implementation "com.google.android.gms:play-services-location:$firebaseVersion"


    //Used for testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.8.9'
    testImplementation 'org.assertj:assertj-core:1.7.0'
    testImplementation 'org.robolectric:robolectric:3.5.1'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

apply plugin: 'com.google.gms.google-services'

//Fancy printing for test cases
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

tasks.withType(Test) {
    testLogging {
        // set options for log level LIFECYCLE
        events TestLogEvent.FAILED,
                TestLogEvent.PASSED,
                TestLogEvent.SKIPPED,
                TestLogEvent.STANDARD_OUT
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events TestLogEvent.STARTED,
                    TestLogEvent.FAILED,
                    TestLogEvent.PASSED,
                    TestLogEvent.SKIPPED,
                    TestLogEvent.STANDARD_ERROR,
                    TestLogEvent.STANDARD_OUT
            exceptionFormat TestExceptionFormat.FULL
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}
