apply plugin: 'findbugs'

afterEvaluate {
    def variants = plugins.hasPlugin('com.android.application') ?
            android.applicationVariants : android.libraryVariants

    variants.each { variant ->
        def task = tasks.create("findBugs${variant.name.capitalize()}", FindBugs) {
            group = 'verification'
            description = "Run FindBugs for the ${variant.description}."
            ignoreFailures = true
            effort = 'max'
            reports {
                xml.enabled = true
                html.enabled = false
            }

            def variantCompile = variant.javaCompileProvider.get()
            classes = fileTree(variantCompile.destinationDir)
            source = variantCompile.source
            classpath = variantCompile.classpath.plus(project.files(android.bootClasspath))
            dependsOn(variantCompile)
            excludeFilter = new File("$rootDir/gradle/config/excludeBugsFilter.xml")
        }
        tasks.getByName('check').dependsOn(task)
    }
}
