apply plugin: 'pmd'

task pmd(type: Pmd) {
    description 'Run PMD'

    ruleSetFiles = files("../pmd-ruleset.xml")
    ruleSets = []

    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = true
        html.enabled = false
    }

    ignoreFailures = true
}

afterEvaluate {
    def variants = plugins.hasPlugin('com.android.application') ?
            android.applicationVariants : android.libraryVariants

    variants.each { variant ->
        def task = tasks.create("pmd${variant.name.capitalize()}", Pmd) {
            ruleSetFiles = files("../pmd-ruleset.xml")
            ruleSets = []

            source 'src'
            include '**/*.java'
            exclude '**/gen/**'

            reports {
                xml.enabled = false
                html.enabled = true
            }

            ignoreFailures = true

            dependsOn(variant.javaCompile)
        }
        tasks.getByName('check').dependsOn(task)
    }
}

// PMD's duplicate code tool
cpd {
    language = 'java'
}

cpdCheck {
    reports {
        text.enabled = false
        xml.enabled = true
        xml.destination file("${buildDir}/reports/pmd/duplicates.xml")
    }
    source = 'src/main/java'
    exclude "**/*.kt"
    ignoreFailures = true
}

check.dependsOn('cpdCheck')